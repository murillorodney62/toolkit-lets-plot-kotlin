/*
 * Copyright (c) 2020. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id "com.jfrog.bintray"
    id 'org.jetbrains.dokka' version '0.10.0'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$projectDir/../docs/api-reference"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile "junit:junit:$junit_version"

    api "org.jetbrains.lets-plot:lets-plot-common:$lets_plot_version"
    compileOnly "org.jetbrains.lets-plot:lets-plot-image-export:$lets_plot_version"
}


def artifactBaseName = 'lets-plot-kotlin-api'
def artifactGroupId = project.group
def artifactVersion = project.version

def task_group = 'lets plot'

task jarClasses(type: Jar, dependsOn: classes) {
    group task_group
    baseName artifactBaseName
    version artifactVersion
    from sourceSets.main.output
}

task jarSources(type: Jar) {
    classifier 'sources'
    group task_group
    baseName artifactBaseName
    version artifactVersion
    from sourceSets.main.allSource
}


publishing {
    publications {
        letsPlotKotlinApi(MavenPublication) {
            groupId artifactGroupId
            artifactId artifactBaseName
            version artifactVersion

            artifact jarClasses
            artifact jarSources

            pom {
                name = "Lets-Plot Kotlin API"
                description = "Lets-Plot Kotlin API"
                url = "https://github.com/JetBrains/lets-plot-kotlin"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot-kotlin"
                }
                // Add dependency on lets-plot-common
                withXml {
                    def deps = asNode().appendNode('dependencies')
                    def dep = deps.appendNode('dependency')
                    dep.appendNode('groupId', 'org.jetbrains.lets-plot')
                    dep.appendNode('artifactId', 'lets-plot-common')
                    dep.appendNode('version', lets_plot_version)
                }
            }
        }

        letsPlotKotlinApiKernel(MavenPublication) {
            groupId artifactGroupId
            artifactId "$artifactBaseName-kernel"
            version artifactVersion

            artifact jarClasses
            artifact jarSources

            pom {
                name = "Lets-Plot Kotlin API (for Jupyter Kotlin Kernel)"
                description = "Lets-Plot Kotlin API without fixed dependencies."
                url = "https://github.com/JetBrains/lets-plot-kotlin"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "jetbrains"
                        name = "JetBrains"
                        email = "lets-plot@jetbrains.com"
                    }
                }
                scm {
                    url = "https://github.com/JetBrains/lets-plot-kotlin"
                }
            }
        }
    }

    repositories {
        maven {
            url = uri("https://jetbrains.bintray.com/lets-plot-maven")
        }
    }
}

// Provide Bintray-specific Information
def bintray_user = project.buildSettings?.bintray?.user
def bintray_key = project.buildSettings?.bintray?.key

if (bintray_user != null && bintray_key != null) {

    def pkg_name = 'lets-plot-kotlin-jars'
    if (ext.versionIsDev) {
        pkg_name = 'lets-plot-kotlin-jars-dev'
    }

    bintray {
        user = bintray_user
        key = bintray_key
        publications = ['letsPlotKotlinApi', 'letsPlotKotlinApiKernel']
        publish = true
        override = true

        pkg {
            repo = 'lets-plot-maven'
            name = pkg_name
            userOrg = 'jetbrains'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JetBrains/lets-plot-kotlin'
            version {
                name = artifactVersion
            }
        }
    }
}
