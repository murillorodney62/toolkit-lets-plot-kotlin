buildscript {
    dependencies {
        classpath("org.yaml:snakeyaml:1.25")
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "com.jfrog.bintray" version '1.8.4' apply false
}

/*
 * Copyright (c) 2021. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

import org.yaml.snakeyaml.Yaml

def build_settings_file = new File(rootDir, "build_settings.yml")
if (!build_settings_file.canRead()) {
    throw new GradleException("Couldn't read build_settings.yml")
}
def settings = new Yaml().load(build_settings_file.newInputStream())
project.ext.buildSettings = settings


allprojects {
    group 'org.jetbrains.lets-plot-kotlin'
    version "1.1.1-alpha12"

    ext.versionIsDev = (version.contains('SNAPSHOT')
            || version.contains('rc')
            || version.contains('alpha')
            || version.contains('beta'))

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        maven {
            // GeoTools repository must be before Maven Central
            // See: https://stackoverflow.com/questions/26993105/i-get-an-error-downloading-javax-media-jai-core1-1-3-from-maven-central
            // See also Jupyter Kotlin issue: https://github.com/Kotlin/kotlin-jupyter/issues/107
            url "https://repo.osgeo.org/repository/release"
        }

        mavenCentral()
        jcenter()

//        mavenLocal()

//        maven {
//            url "file:///Users/Igor/Work/lets-plot/.maven-publish-dev-repo"
//        }

        maven {
            // Required when working with SNAPSHOT-s
            url "https://jetbrains.bintray.com/lets-plot-maven"
        }
    }
}